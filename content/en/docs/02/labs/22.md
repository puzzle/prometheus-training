---
title: "2.2 Tasks: PromQL"
weight: 2
sectionnumber: 2.2
---

In this lab you are going to learn a bit more about [PromQL (Prometheus Query Language)](https://prometheus.io/docs/prometheus/latest/querying/basics/).

PromQL is the query language that allows you to select, aggregate and filter the time series data collected by prometheus in real time.

{{% alert title="Note" color="primary" %}}
PromQL can seem overwhelming. It may take a little time to get used to it.
{{% /alert %}}

### Task {{% param sectionnumber %}}.1: Explore Examples

In this first task you are going to explore some querying examples.

Get all time series with the metric `prometheus_http_requests_total`.

```promql
prometheus_http_requests_total
```

The result represents the time series for the http requests sent to your Prometheus server as a **instant vector**.

Get all time series with the metric `prometheus_http_requests_total` and the given `code` and `handler` labels:

```promql
prometheus_http_requests_total{code="200", handler="/api/v1/query"}
```

The result will show you the time series for the http requests sent to the query endpoint of your Prometheus Server, which were successful ( HTTP status code 200 ).

Get a whole range of time (5 minutes) for the same vector, making it a **range vector**:

```promql
prometheus_http_requests_total{code="200", handler="/api/v1/query"}[5m]
```

A range vector can not be graphed directly in the Prometheus UI, use the console view to display the result.

With regular expressions you can filter time series only for `handlers` whose name matches a certain pattern, in this case all `handlers` starting with `/api`:

```promql
prometheus_http_requests_total{handler=~"/api.*"}
```

All regular expressions in Prometheus use the [RE2 syntax](https://github.com/google/re2/wiki/Syntax).
To select all HTTP status codes except 2xx, you would execute:

```promql
prometheus_http_requests_total{code!~"2.."}
```

### Task {{% param sectionnumber %}}.2: Sum Aggregation Operator

The [Prometheus Aggregation operators](https://prometheus.io/docs/prometheus/latest/querying/operators/#aggregation-operators) help us to aggregate time series in PromQL.

There is a Prometheus metric that represents all samples scraped  by Prometheus

Use a [Prometheus Aggregation operators](https://prometheus.io/docs/prometheus/latest/querying/operators/#aggregation-operators) to sum the returned metrics

{{% details title="Hints" mode-switcher="normalexpertmode" %}}

The metric `scrape_samples_scraped` represents the total of scraped samples by `job` and `instance`. To get the total amount of scrapped samples, we use the [Prometheus aggregation operators](https://prometheus.io/docs/prometheus/latest/querying/operators/#aggregation-operators) `sum` to sum the values.

```promql
sum(scrape_samples_scraped)
```

{{% /details %}}

### Task {{% param sectionnumber %}}.3: Rate Function

Use the `rate()` function to display the current CPU **idle** usage of the Prometheus server in % based on data of the last 5 minutes.

{{% alert title="Hint" color="primary" %}}
Read the [documentation](https://prometheus.io/docs/prometheus/latest/querying/functions/) about the `rate()` function.
{{% /alert %}}

{{% details title="Hints" mode-switcher="normalexpertmode" %}}

The CPU metrics are collected and exposed by the `node_exporter` therefore the metric we're looking for is under the `node` namespace.

```promql
node_cpu_seconds_total
```

To get the `idle` CPU seconds, we add the label filter `{mode="idle"}`.

Since the `rate` function calculates the per-second average increase of the time series in a **range vector**, we have to pass a range vector to the function `node_cpu_seconds_total{mode="idle"}[5m]`

To get the idle usage in % we therefore have to multiply it with 100.

```promql
rate(
  node_cpu_seconds_total{mode="idle"}[5m]
  )
* 100
```

{{% /details %}}

### Task {{% param sectionnumber %}}.4: Arithmetic Binary Operator

In the previous lab, we created a query that returns the CPU **idle** usage.
Now let's reuse that query to create a query that returns the current CPU usage of the Prometheus server in %. The usage is the total (100%) [minus](https://prometheus.io/docs/prometheus/latest/querying/operators/#arithmetic-binary-operators) the CPU usage **idle**.

{{% details title="Hints" mode-switcher="normalexpertmode" %}}

To get the CPU usage we can simply substract **idle** CPU usage from 1 (100%) and then multiply it by 100 to get percentage.

```promql
(
  1 -
  rate(
      node_cpu_seconds_total{mode="idle"}[5m]
      )
)
* 100
```

{{% /details %}}

### Task {{% param sectionnumber %}}.5: How much free memory

[Arithmetic Binary Operator](https://prometheus.io/docs/prometheus/latest/querying/operators/#arithmetic-binary-operators) can not only be used with constant values `eg. 1`, it can also be used to evaluate to other instant vectors.

Write a Query that returns how much of the memory is free in %.

The `node exporter` exposes these two metrics:

* `node_memory_MemTotal_bytes`
* `node_memory_MemAvailable_bytes`

{{% details title="Hints" mode-switcher="normalexpertmode" %}}

We can simply divide the available memory metric by the total memory of the node and multiply it by 100 to get percent.

```promql
sum by(instance) (node_memory_MemAvailable_bytes)
/
sum by(instance) (node_memory_MemTotal_bytes)
* 100
```

{{% /details %}}

### Task {{% param sectionnumber %}}.6: Comparison Binary Operators

In addition to the Arithmetic Binary Operator, PromQL also provides a set of [Comparison binary operators](https://prometheus.io/docs/prometheus/latest/querying/operators/#comparison-binary-operators)

* `==` (equal)
* `!=` (not-equal)
* `>` (greater-than)
* `<` (less-than)
* `>=` (greater-or-equal)
* `<=` (less-or-equal)

Check if the Prometheus server has more than 20% memory available using a [Comparison binary operators](https://prometheus.io/docs/prometheus/latest/querying/operators/#comparison-binary-operators)

{{% details title="Hints" mode-switcher="normalexpertmode" %}}

We can simply use the greater-than-binary operator to compare the instant vector from the query with 20 (In our case, this corresponds to 20% memory usage).

```promql
sum by(instance) (node_memory_MemAvailable_bytes)
/
sum by(instance) (node_memory_MemTotal_bytes)
* 100
> 20
```

The query only has a result when more than 20% of the memory is available.

Change the value from 20 to 90 or more to see the result, when the operator doesn't match.

{{% /details %}}

### Task {{% param sectionnumber %}}.7: Histogram

So far we've using gauge and counter metric types in our queries.

Read the [documentation](https://prometheus.io/docs/concepts/metric_types/#histogram) about the `histogram` metric type.

There exists a histogram for the http request durations to the Prometheus sever. It basically counts requests that took a certain amount of time and puts them into matching buckets (`le` label).

We want to write a query that returns

* the total numbers of requests
* to the Prometheus server
* on `/metrics`
* below 0.1 seconds

{{% details title="Hints" mode-switcher="normalexpertmode" %}}

As seen in previous labs, the http metrics for the Prometheus server are available in the `prometheus` namespace.

By filtering the `le` label to 0.1 we get the result for our query.

```promql
prometheus_http_request_duration_seconds_bucket{handler="/metrics",le="0.1"}
```

Tip: Analyze the query in [PromLens](https://demo.promlens.com/)

You can calculate how many requests in % were below 0.1 seconds by aggregating above metric. See more information about Apdex score at [Prometheus documentation](https://prometheus.io/docs/practices/histograms/#apdex-score)

Example

```promql
sum(
  rate(
    prometheus_http_request_duration_seconds_bucket{handler="/metrics",le="0.1"}[5m]
  )
) by (job, handler)
/
sum(
  rate(
    prometheus_http_request_duration_seconds_count{handler="/metrics"}[5m]
  )
) by (job, handler)
* 100
```

{{% /details %}}

### Task {{% param sectionnumber %}}.8: Quantile

We can use the [histogram_quantile](https://prometheus.io/docs/prometheus/latest/querying/functions/#histogram_quantile) function to calculate the request duration quantile of the requests to the Prometheus server from a histogram metric. To archive this we can use the metric `prometheus_http_request_duration_seconds_bucket`, which the Prometheus server exposes by default.

Write a query, that returns the per-second average of the 0.9th quantile under the `metrics` handler using the metric mentioned above.

{{% details title="Hints" mode-switcher="normalexpertmode" %}}

Expression

```promql
histogram_quantile(
  0.9,
  rate(
    prometheus_http_request_duration_seconds_bucket{handler="/metrics"}[5m]
  )
)
```

Explanation: `histogram_quantile` will calculate the 0.9 quantile based on the samples distribution in our buckets by assuming a linear distribution within a bucket.

{{% /details %}}

### Task {{% param sectionnumber %}}.9: predict_linear function (optional)

We could simply alert on static thresholds. For example, notify when the file system is more than 90% full. But sometimes 90% disk usage is a desired state. For example, if our volume is very large. (e.g. 10% of 10TB would still be 1TB free, who wants to waste that space?) So it is better to write queries based on predictions. Say, a query that tells me that my disk will be full within the next 24 hours if the growth rate is the same as the last 6 hours.

Let's write a query, that exactly makes such predictions:

* Find a metric that displays you the available disk space on filesystem mounted on `/`
* Use a function that allows you to predict when the filesystem will be full in 4 hours
* Predict the usage linearly based on the growth over the last 1 hour

{{% details title="Hints" mode-switcher="normalexpertmode" %}}

Expression

```promql
predict_linear(node_filesystem_avail_bytes{mountpoint="/"}[1h], 3600 * 4) < 0
```

Explanation: based on data over the last `1h`, the disk will be `< 0` bytes in `3600 * 4`s.
The query will return `no data` because the file system will not be full in the next 4 hours. You can check how much disk space will be available in 4 hours by removing the `< 0` part.

```promql
predict_linear(node_filesystem_avail_bytes{mountpoint="/"}[1h], 3600 * 4)
```

{{% /details %}}

### Task {{% param sectionnumber %}}.10: (optional)

Prometheus provides built-in metrics that can be used to correlate their values with metrics exposed by your exporters. One such metric is `date()`.
Prometheus also allows you to add more labels from different metrics if you can correlate both metrics by labels. See [Many-to-one and one-to-many vector matches](https://prometheus.io/docs/prometheus/latest/querying/operators/#many-to-one-and-one-to-many-vector-matches) for more examples.

Write a query that answers the following questions:

* What is the uptime of my VM in minutes?
* Which kernel is currently active?

{{% details title="Hints" mode-switcher="normalexpertmode" %}}

Expression

```promql
(
  (
    time() - node_boot_time_seconds
  ) / 60
)
* on(instance) group_left(release) node_uname_info
```

* __time()__: Use the current UNIX Epoch time
* __node_boot_time_seconds__: Returns the UNIX epoch time at which the VM was started
* __on(instance) group_left(release) node_uname_info__: Group your metrics result with the metric `node_uname_info` which contains information about your kernel in the `release` label.

Alternative solution would be:

```promql
node_uname_info * on(instance) group_right(release)
(
  (
    time() - node_boot_time_seconds
  ) / 60
)
```

{{% /details %}}
